Function MyDebug(...)

	Local Function PrettyPrint(item, name, indent)

		ConsolePrintNR(RepeatStr(" ", indent))

		If GetType(item) = #TABLE
			If StrLen(name) <> 0
				ConsolePrintNR(name, " : ")
			EndIf
		Else
			If StrLen(name) = 0
				ConsolePrintNR(item, " ")
			Else
				ConsolePrintNR(name, " : ", item)
			EndIf	
		EndIf
	
		Switch GetType(item)
			Case #NUMBER
				ConsolePrint("")
			Case #STRING
				ConsolePrint("")
			Case #TABLE
				ConsolePrint("{")

				If HaveItem(item, 0)
					; Normal num√©ric index based array
					For k,v In IPairs(item)
						PrettyPrint(v, k, indent + 2)
					Next
				Else
					; Associative array
					For k,v In Pairs(item)
						PrettyPrint(v, k, indent + 2)
					Next
				EndIf
		 
				ConsolePrintNR(RepeatStr(" ", indent) .. "}")
				
				If StrLen(name) <> 0
					ConsolePrint(" // ", name)
				Else
					ConsolePrint("")
				EndIf
				
			Case #FUNCTION
				ConsolePrint(" (function)")
			Case #USERDATA
				ConsolePrint(" (userdata)")
			Case #LIGHTUSERDATA
				ConsolePrint(" (lightuserdata)")
			Case #THREAD
				ConsolePrint(" (thread)")
			Case #NIL
				ConsolePrint(" (nil)")
		EndSwitch
	
	
	EndFunction

	For k, v In IPairs(arg)
		PrettyPrint(v, "param" .. k+1, 0)
		ConsolePrint("")
	Next
	
EndFunction
